/*
Base fields:

orbit 	35j:						Q(a)
orbit   35i: 						Q(a)
orbit	35h:						Q(a)
orbit	35e:						Q(a)
orbits	35af:						Q(a,r3)
orbits	35bcdg:						Q(a,r3,b5)

SIC fields:

orbit 	35j (ray class field):		Q(a,r1,b1,b2,b3,b4,i*r2)			(degree 288)
orbit   35i: 						Q(a,r1,r2,b1,b2,b3,b4,i)			(degree 576)
orbit	35h:						Q(a,r1,r2,b1,b2,b3,b4,b6,i)			(degree 1152)
orbit	35e:						Q(a,r1,r2*r3,b1,b2,b3,b4,c,i*r2)	(degree 1152)
orbits	35af:						Q(a,r1,r2,r3,b1,b2,b3,b4,b5*b6,i)	(degree 2304)
orbits	35bcdg:						Q(a,r1,r2,r3,b1,b2,b3,b4,b5,b6,i)	(degree 4608)
*/

P0<x0>:=PolynomialRing(RationalField());

Fa<a>:=NumberField(x0^2-2);
Pa<xa>:=PolynomialRing(Fa);

Fr1<r1>:=NumberField(xa^2-5);
Pr1<xr1>:=PolynomialRing(Fr1);

Fr2<r2>:=NumberField(xr1^2-7);
Pr2<xr2>:=PolynomialRing(Fr2);

Fr3<r3>:=NumberField(xr2^2-3);
Pr3<xr3>:=PolynomialRing(Fr3);

Fb2<b2>:=NumberField(xr3^3-210*xr3-560);
Pb2<xb2>:=PolynomialRing(Fb2);

Fb3<b3>:=NumberField(xb2^2-14*(r1+5));
Pb3<xb3>:=PolynomialRing(Fb3);

Fb4<b4>:=NumberField(xb3^3 - 21*xb3 - 7: Check:=false); // ~2 sec to check irreducibility
Pb4<xb4>:=PolynomialRing(Fb4);

Fb5<b5>:=NumberField(xb4^2-(3-r3): Check:=false); // ~20 sec to check irreducibility
Pb5<xb5>:=PolynomialRing(Fb5);

Fb6<b6>:=NumberField(xb5^2 + a*(5-b3/(a*r2)-r1):Check:=false);  // ~3000 sec to check irreducibility
Pb6<xb6>:=PolynomialRing(Fb6);

Fb1<b1>:=NumberField(xb6^2 + 2*a + 1:Check:=false); // ~ 70000 sec to check irreducibility (i.e. ~ 1 day)
Pb1<xb1>:=PolynomialRing(Fb1);

F<i>:=NumberField(xb1^2+1: Check:=false);  //  Irreducibility not checked directly.  For indirect check see below
P<x>:=PolynomialRing(F);

ir2:=i*r2; // this generator features in the fields for fiducials 35e, 35j

c:=((r2*r3 + (a*r1 + (2*a + 1))*r2)*b3 + (42*r1 + 70))*b5*b6; // this generator features in the field for fiducial 35e

tau:=((-1/504*r2*b3+(-1/168*r1+1/168)*r2)*b4^2+1/252*r2*b3*b4+(17/504*r2*b3+(1/24*r1-1/24)*r2))*i+(-1/168*b3+(1/72*r1-1/72))*b4^2+(-1/36*r1+1/36)*b4+1/24*b3+-17/72*r1+17/72;

/*
CHECKING IRREDUCIBILTY OF DEFINING POLYNOMIAL OF F

A direct check is likely to be somewhat time consuming.  So we did an indirect check instead.

Consider the subfields

Fb1	= Q(a,r1,r2,b3,b4,b6,b1)  	(degree 192)
F   = Q(r3,b5,i)				(degree 8)
Fb2 = Q(b2)						(degree 3)

Consider first G = Fb1 ∩ F.  The degree of G must divide GCD(192,8) = 8.  
We have that i,r3 ∉ G (11 seconds each to check). 
The defining polynomials of the proper subfields of F are

sfds:=[
x0^8 - 8*x0^6 + 12*x0^5 + 21*x0^4 - 120*x0^3 + 226*x0^2 - 204*x0 + 73,
x0^2 + x0 + 1,
x0^2 + 1,
x0^2 - 3,
x0^4 - x0^2 + 1,
x0^4 - 6*x0^2 + 6,
x0^4 + 6*x0^2 + 6
];

We find that there is only one subfield not containing i and r3, namely the one generated by 
x0^2 + x0 +1 which is Q(i√3).  i√3 is not contained in Fb1 (12 seconds).  So G = Q.  It follows that 
F ∨ Fb1= Q(a,r1,r2,r3,b3,b4,b5,b6,b1,i) is degree 8*192 = 1536.

Now consider G = Fb2 ∧ (F ∨ Fb1).  The degree of G must divide GCD(1536,3) = 3.  If Degree(G) was 3 it 
would mean that b2 ∈ F ∨ Fb1 = Q(a,r1,r2,r3,b3,b4,b5,b6,b1,i).  But that would imply 
Q(a,r1,b2,b3,b4,b1,i*r2) was a subfield of  F ∨ Fb1.  In particular, it would mean its degree was 
a divisor of 1536. However, Q(a,r1,b2,b3,b4,b1,i*r2) is the ray class field, with degree 9*32=288, and 288 
is a not a divisor of 1536.  We conclude that Degree(G) is 1, implying that
 Q(a,r1,r2,r3,b2,b3,b4,b5,b6,b1,i)) is degree 4608, and that each of the defining polynomials in the tower in
 d35Field.mgm is irreducible over the field below it.

*/



